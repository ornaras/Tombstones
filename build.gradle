plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("org.sonarqube") version "7.1.0.6387"
}

group = 'ru.ornaras'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.19")
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.author = true
    options.version = true

    options.links(
            'https://docs.oracle.com/en/java/javase/17/docs/api/',
            'https://hub.spigotmc.org/javadocs/spigot/'
    )

    options.addStringOption('Xdoclint:none', '-quiet')
    classpath += configurations.compileClasspath
}

tasks.register('javadocJar', Jar) {
    dependsOn tasks.javadoc
    from tasks.javadoc
    archiveClassifier.set('javadoc')
}

artifacts {
    archives tasks.javadocJar
}